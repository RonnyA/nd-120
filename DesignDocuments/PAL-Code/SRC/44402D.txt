PAL16R4
JLB/CJTC 8AUG86
44402D,18F,UBITS

UCLK /DT /RT LSHADOW FMISS CYD /HIT0 /HIT1 /EWC GND
/OE OUBI OUBD /IHIT NC15 NUBD NUBI /WCA /USED VCC

; CACHE USED BIT CONTROL

IHIT := HIT1 * HIT0 * OUBI * RT * /DT * /WCA        ; FETCH
      + HIT1 * HIT0 * OUBD * RT * DT * /WCA         ; READ


IF (VCC) USED = OUBI * RT * /DT * /WCA              ; FETCH
              + OUBD * RT * DT * /WCA               ;READ


IF (VCC) WCA = /RT * DT * EWC * CYD * /FMISS * /LSHADOW         ; WRITE OUTSIDE SHADOW
              + RT * /IHIT * EWC * CYD * /FMISS * /LSHADOW      ; FETCH/READ WITHOUT HIT
                                                                ; OUTSIDE SHADOW

/NUBI := DT * /OUBI             ; READ OR WRITE KEEP OLD VALUE
       + /RT * DT * HIT0 * HIT1 ; CLEAR IF WRITE WITH HIT

/NUBD := RT * /DT * /OUBD       ; IF FETCH KEEP OLD VALUE

DESCRIPTION

;B 050387 JLB: USED BITS INVERTED TO ACTIVE HIGH.
;C 060387 JLB: SHOULD NOT PRODUCE WCA WHEN IN SHADOW.
;D 120387 JLB: HIT PATH IMPROVED

; 180576 M3202B
;B 300687 JLB: CYD POSITIVE POLARITY.
;C 130787 JLB: HOLD TERM ON WCA REMOVED BECAUSE CYD FROM d TO f.
;D 030987 JLB: TERM REMOVED AS AN INPUT, FMISS NEW INPUT PIN 5.
;              WCA SHOULD NOT APPEAR WHEN FMISS (TSET FAILS).
